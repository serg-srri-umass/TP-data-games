<?xml version="1.0" encoding="utf-8"?>
<!-- fonts/DeviceFont.mxml -->
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   width="575" height="325"
			   creationComplete="testFlashPlayer()" 
			   pageTitle="Inference Games"
			   activate="ScriptInterface.signalActivation()" 
			   deactivate="ScriptInterface.signalDeactivation()"
			   backgroundColor="#CCCCCC">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import flash.external.ExternalInterface;
			import flash.utils.Timer;
			
			import common.DGInterface;
			import common.ParkMiller;
			import common.ScriptInterface;
			import common.VersionChecker;
			
			import embedded_asset_classes.DebugConsoleSWC;
			import embedded_asset_classes.InferenceEvent;
			import embedded_asset_classes.SpaceRace;
			
			// ----------------------
			// --- PRIVATE SECTION ---
			// ----------------------
						
			private static const kMinimumRequiredVersion:Number = 11.6;	// the oldest version of flash player you can play this game on.
			private static const kDebugMode:Boolean = true; 			// set this to false, and the debug version will behave exactly like the release version.
			private static const kVersion:String = "v.1.14";
			private static const kEventsCollectionName:String = "Events";
			private static const kGamesCollectionName:String = "Games";
			private static const kCreditsTextHTML:String = 
				"<b>Inference</b> is a <font color='#2C4877'><u><a href='http://www.srri.umass.edu/datagames' target='inferenceInfo'>Data Game</a></u></font> "+
				"created by the <font color='#2C4877'><u><a href='http://www.srri.umass.edu/serg' target='inferenceInfo'>Statistics Education Research Group</a></u></font> "+
				"at <font color='#2C4877'><u><a href='http://www.srri.umass.edu' target='inferenceInfo'>SRRI</a></u></font>. "+
				"Contributers to the concept, design and development include the " +
				"Research and Activities team: Clifford Konold, Kosoom (Jang) Kreetong; "+
				"Developers: Craig D. Miller; "+
				"Student Programmers: Russ Phelan, Mike Salyh, Ryan McCann. "+
				"Artist: Mike Salyh. "+
				"Funded by the National Science Foundation, award "+
				"<font color='#2C4877'><u><a href='http://www.nsf.gov/awardsearch/showAward?AWD_ID=0918653&amp;HistoricalAwards=false' target='inferenceInfo'>DRL-0918653</a></u></font> (2009-2014). "+
				"Inference is open source on <font color='#2C4877'><u><a href='https://github.com/serg-srri-umass/TP-data-games/wiki' target='inferenceInfo'>Github</a></u></font>. "+
				"Copyright (c) 2013<span class='x'>-2014 by University of Massachusetts Amherst.";
			private static const kGameDescriptionText:String = "Guess the population mean from samples, using the allowable tolerance and standard deviation as your guide.";
			
			private static var staticStage:Stage; // a reference to the stage of this MXML file.

			private var sDebugConsole:DebugConsoleSWC;
			public var sSpaceRace:SpaceRace;
			private var sExpertAI:ExpertAI;
			private var videoPlayer:Object;	// The Youtube video player for the intro video
			private var sThinkingCursor:ThinkingCursor;

			private var mDGInterface:DGInterface = null;	// interface to Data Games container application (if present)	
			private var mGameSerialNum:int	= 0; // first game starts with serial number 1.
			private var mGameLevel:int		= 1; // which level (1+) is the current game using.
			
			public static var instance:InferenceGames = null;
			public var randomizer:ParkMiller = null;
			public var isInGame:Boolean = false; // whether there is a game currently going on.
			[Bindable]
			public var unlockedLevels:uint = 1; // the player has unlocked levels <= this value.
			public var completedLevels:uint = 0; // the player has completed levels <= this value.
			
			// this method ensures that the flash player is a recent enough version to work.
			private function testFlashPlayer():void {
				if( VersionChecker.isValid(kMinimumRequiredVersion))
					startUp();
				 else
					BrickWall.visible = true;
			}
			
			private function startUp():void{
				if( instance == null )
					instance = this;
				
				this.randomizer = new ParkMiller(); // must instantiate just once to set random seed according to current date-time.
				BrickWall.visible = false;
				
				initVisualElements();
				sExpertAI = new ExpertAI(SpaceRaceControls.INSTANCE);
				
				mDGInterface = new DGInterface( VersionChecker.isDebug && kDebugMode );
				this.sendCollectionData();				// initialize the DG/TinkerPlots data interface (and set up Graph and Table parameters)
				mDGInterface.createGraphIfNone();		// create a Graph view in DG, if not present already
				
				//listen for events from Space Race Module 
				sSpaceRace.addEventListener(InferenceEvent.REQUEST_NEW_ROUND, newRoundHandler);
				sSpaceRace.addEventListener(InferenceEvent.REQUEST_SAMPLE, requestSampleHandler);

				sSpaceRace.addEventListener(InferenceEvent.REQUEST_NEW_GAME, newGame);
				sSpaceRace.addEventListener(InferenceEvent.REQUEST_END_GAME, endGame);
				
				sSpaceRace.addEventListener(InferenceEvent.CORRECT_GUESS, handleCorrectGuess);
				sSpaceRace.addEventListener(InferenceEvent.INCORRECT_GUESS, handleIncorrectGuess);				

				sSpaceRace.addEventListener(InferenceEvent.REQUEST_GUESS_MODE_EXPERT, sSpaceRace.prepareGuessExpert);
				sSpaceRace.addEventListener(InferenceEvent.REQUEST_GUESS_MODE_HUMAN, sSpaceRace.prepareGuessHuman);
				
				sSpaceRace.addEventListener(InferenceEvent.REMOVE_FOCUS, sSpaceRace.removeFocus);
				sSpaceRace.addEventListener(InferenceEvent.EXPERT_START_TURN, showExpertCursor);
				sSpaceRace.addEventListener(InferenceEvent.REQUEST_HUMAN_CURSOR, showHumanCursor);

				//hide StDev & tolerance bars on startup
				sSpaceRace.bodyMVC.setPossibleSDs();
				sSpaceRace.bodyMVC.setPossibleTolerances();
			}
			
			// called after Human or Expert guess is made, to end the round, see also handleIncorrectGuess()
			private function handleCorrectGuess( triggerEvent:Event = null):void{
				showHumanCursor();
				
				var headerText:String = "", bodyText:String = "";
				
				// get end of round feedback and score points
				if(sSpaceRace.activePlayerIsHuman){ //you
					headerText = "Your guess was correct";
					Round.currentRound.setResultString("You won");
					if( Round.currentRound.wasLucky())
						bodyText = "You got lucky";
					sSpaceRace.earnPointHuman();
				} else { // expert
					headerText = "The expert's guess was correct";
					Round.currentRound.setResultString("Expert won");
					if( Round.currentRound.wasLucky())
						bodyText = "The expert got lucky";
					sSpaceRace.earnPointExpert();
				}
				
				// get end of Game feedback and possibly unlock levels
				var isExpertWin:Boolean = (sSpaceRace.expertScore >= sSpaceRace.WINNING_SCORE),
					isUserWin:Boolean =   (sSpaceRace.humanScore  >= sSpaceRace.WINNING_SCORE);
				if( isExpertWin || isUserWin ){
					showEndOfGameExpert( isUserWin);
					if(isUserWin){
						headerText = "Correct, you won the game!";
						bodyText = "";
						if( checkAndUnlockLevel( isUserWin )) {
							if( mGameLevel == Round.kLevelSettings.length ) 
								bodyText = "and completed all levels";
							else
								bodyText = "and unlocked level "+unlockedLevels;
						}
					}
					else{
						headerText = "The expert was correct and won the game";
					}
				}
				
				// set messages and buttons
				if( isExpertWin || isUserWin ){
					sSpaceRace.bodyMVC.showFeedback( headerText, bodyText, false);	// bring up the feedback screen
				}else{
					sSpaceRace.bodyMVC.showFeedback( headerText, bodyText, true, "Next Round");
				}
				
				endRound();
			}
			
			// called after Human or Expert guess is made, to end the round, see also handleCorrectGuess()		
			private function handleIncorrectGuess( triggerEvent:Event = null):void{
				showHumanCursor();
				
				var headerText:String = "", bodyText:String = "";
				
				// get end of round feedback and score points
				if(sSpaceRace.activePlayerIsHuman){
					headerText = "You missed";
					Round.currentRound.setResultString("You lost");
					if( Round.currentRound.wasUnlucky())
						bodyText = "You were unlucky, keep trying";
					sSpaceRace.earnPointExpert();
					sSpaceRace.earnPointExpert();
				} else {
					headerText = "The expert missed";
					Round.currentRound.setResultString("Expert lost");
					if( Round.currentRound.wasUnlucky())
						bodyText = "The expert was unlucky";
					sSpaceRace.earnPointHuman();
					sSpaceRace.earnPointHuman();
				}
				
				// get end of Game feedback and possibly unlock levels
				var isExpertWin:Boolean = (sSpaceRace.expertScore >= sSpaceRace.WINNING_SCORE),
					isUserWin:Boolean =   (sSpaceRace.humanScore  >= sSpaceRace.WINNING_SCORE);
				if( isExpertWin || isUserWin){
					showEndOfGameExpert( isUserWin);
					if(isUserWin){
						headerText = "Expert missed, you won the game";
						bodyText = "";
						if( checkAndUnlockLevel( isUserWin )) {
							if( mGameLevel == Round.kLevelSettings.length ) 
								bodyText = "and completed all levels";
							else
								bodyText = "and unlocked level "+unlockedLevels;
						}
					}
					else{
						headerText = "You missed, the expert won the game";
					}
				}
				
				// set messages and buttons
				if( isExpertWin || isUserWin ){
					sSpaceRace.bodyMVC.showFeedback( headerText, bodyText, false);	// bring up the feedback screen
				}else{
					sSpaceRace.bodyMVC.showFeedback( headerText, bodyText, true, "Next round");
				}
				
				endRound();
			}
		
			// Makes the expert movie clip pop up. Used at the end of games.
			private function showEndOfGameExpert( isUserWin:Boolean ):void{
				
				if( isUserWin){
					if( sSpaceRace.expertScore < 2){
						// show defeated expert
						sSpaceRace.bodyMVC.popupExpertMVC.gotoAndPlay("show");
						sSpaceRace.bodyMVC.popupExpertMVC.checkovMVC.gotoAndStop("sad");
					}
					// else down show expert
				} else {
					sSpaceRace.bodyMVC.popupExpertMVC.gotoAndPlay("show");
					if( sSpaceRace.humanScore < 2){
						// show happy face
						sSpaceRace.bodyMVC.popupExpertMVC.checkovMVC.gotoAndStop("happy");
					} else {
						// show neutral face
						sSpaceRace.bodyMVC.popupExpertMVC.checkovMVC.gotoAndStop("neutral");
					}
				}
			}
			
			// check for and if needed unlock the next level.  To be called when the score reaches WINNING_SCORE.
			public function checkAndUnlockLevel( isUserWin:Boolean = true):Boolean{
				var wantLevelUnlocked:Boolean = ( isUserWin && mGameLevel == unlockedLevels);
				if( wantLevelUnlocked) { 
					// if the player beats their current level, unlock the next.
					unlockedLevels++;
				}
				if( isUserWin && completedLevels < mGameLevel ) {
					completedLevels = mGameLevel;
				}
				return wantLevelUnlocked;
			}
			
			
			//handles requestNewRound events from SpaceRace interface
			private function newRoundHandler( triggerEvent:Event = null):void{
				// end the old round if there was one
				if(Round.currentRound){
					endRound();
				}
				newRound();
			}
			
			//handles requestSample events from SpaceRace interface
			private function requestSampleHandler(e:Event):void{
				var sampledData:Vector.<Number> = sSpaceRace.sampleData(); // generate the random samples
				Round.currentRound.addData(sampledData); // send data to current round so sample statistics can be calculated
				InferenceGames.instance.sendEventData( sampledData ); // send vector of data to DG for plot and table
			}
			
			private function initVisualElements():void{	
				staticStage = systemManager.stage;
				
				sSpaceRace = new SpaceRace( staticStage, toggleAboutScreen, showVideo);
				gameContainer.addChild(sSpaceRace);
				sSpaceRace.establishLevels( 
					["Level 1", "±"+Round.kLevelSettings.getItemAt(0).sd, "±"+Round.kLevelSettings.getItemAt(0).tolerance,  click_lvl1Btn],
					["Level 2", "±"+Round.kLevelSettings.getItemAt(1).sd, Round.kLevelSettings.getItemAt(1).tolerance,  	click_lvl2Btn],
					["Level 3", Round.kLevelSettings.getItemAt(2).sd, "±"+Round.kLevelSettings.getItemAt(2).tolerance,  click_lvl3Btn],
					["Level 4", Round.kLevelSettings.getItemAt(3).sd, Round.kLevelSettings.getItemAt(3).tolerance,  	click_lvl4Btn]);
				sSpaceRace.bodyMVC.controlsMVC.mainMenuMVC.gameDescriptionTxt.text = kGameDescriptionText;
				
				sDebugConsole = new DebugConsoleSWC();
				gameContainer.addChild(sDebugConsole);
				sDebugConsole.x = 86;
				sDebugConsole.y = 25;

				// set up the cursor for when the expert is thinking
				sThinkingCursor = new ThinkingCursor();
				gameContainer.addChild( sThinkingCursor);
				sThinkingCursor.visible = false;
				sThinkingCursor.mouseEnabled = false;
				sThinkingCursor.mouseChildren = false;
				staticStage.addEventListener( MouseEvent.MOUSE_MOVE, mouseMoveHandler);
				
				initVideoPlayer();
			}

			private function showExpertCursor( triggerEvent:Event = null):void{
				Mouse.hide();
				sThinkingCursor.visible = true;
			}
			
			private function showHumanCursor( triggerEvent:Event = null):void{
				Mouse.show();
				sThinkingCursor.visible = false;
			}
			
			// moves the custom cursor to the mouse's position.
			private function mouseMoveHandler( triggerEvent:MouseEvent):void{
				sThinkingCursor.x = 442; //triggerEvent.stageX;
				sThinkingCursor.y = 206; //triggerEvent.stageY;
			}
			
			// init the 'play video' button & prompt.
			private function initVideoPlayer():void {
				
				//Embedded Youtube video
				var videoID:String = "RnjgJVm4GX0";  // youtube.com video ID (v=) on https://www.youtube.com/user/sergsrri
				
				Security.allowDomain("youtube.com"); //Allow flash to load assets from youtube
				Security.allowDomain("s.ytimg.com");
				
				var loader:Loader = new Loader();
				loader.contentLoaderInfo.addEventListener(Event.INIT, onLoaderInit);
				loader.load(new URLRequest("http://www.youtube.com/apiplayer?version=3"));
				//loader.load(new URLRequest("http://www.youtube.com/v/"+videoID+"?version=3&modestbranding=1&showinfo=1")); //player with controls
				
				function onLoaderInit(event:Event):void {
					videoHolder.addChild(loader);
					loader.content.addEventListener("onReady", onPlayerReady);
					function onPlayerReady(event:Event):void {
						videoPlayer = loader.content;
						videoPlayer.setSize(570, 360);
						videoPlayer.loadVideoById(videoID);
						videoPlayer.pauseVideo();
						videoPlayer.addEventListener("onStateChange", onVideoStateChange);
					}
				}
			}
			
			// ------------------
			// --- DG SECTION ---
			// ------------------
			
			// Send the Game-level and Event-level structure to DG, if connected to DG.  
			// 		The collections are the tables of cases and attributes.
			private function sendCollectionData( ):void	{
				var initGameArgs:Object = 
					{
						name: "InferenceGames",
						dimensions: { width: this.width, height: this.height },
						gameEmbedID: 'InferenceGames', // must match the ID of ChainSaw.swf embedded in the DG page by ChainSaw.html
						gameEmbedFunc: 'doCommandFunc', // callback function name to be used by DG/javascript
						collections: [
							{
								name: kGamesCollectionName,
								attrs:[
									{ name: "Game", type: "numeric", precision: 0, description: "Sequential Game Number" },
									{ name: "Round", type: "numeric", precision: 0, description: "Sequential Round Number" },
									{ name: "Level", type: "numeric", precision: 0, description: "Level of difficulty" },
									{ name: "StDev", type: "numeric", precision: 0, description: "Standard Deviation of distribution" }, 
									{ name: "Tolerance", type: "numeric", precision: 0, description: "How close a guess has to be to the true value" },
									{ name: "SampleSize", type: "numeric", precision: 0, description: "Number of data values generated (sampled) during the round" },
									{ name: "Result", type: "nominal", description: "Who guessed and whether they won or lost." },
									{ name: "ProbWin", type: "numeric", precision: 0, description: "Probability of winning"},
									{ name: "PopMean", type: "numeric", precision: 2, description: "mean of the population we sample data from"},
									{ name: "SampleMean", type: "numeric", precision: 2, description: "mean of the sample data"},
									{ name: "Guess", type: "numeric", precision: 2, description: "guess value entered by you or expert"}
								] ,
								childAttrName: "Events_Record"
							},
							{
								name: kEventsCollectionName,
								attrs:	[
									{ name: "Round", type: "nominal", description: "Which round of the current game"   } ,
									{ name: "Value", type: "numeric", precision: 0, defaultMin: 0, defaultMax: 100, description: "Value of data point generated"   }
								],
								labels: {
									singleCase: "value sent",
									pluralCase: "values sent",
									singleCaseWithArticle: "a value sent",
									setOfCases: "events",
									setOfCasesWithArticle: "events"
								},
								defaults: { xAttr: "Value", yAttr: "Round" }
							}
						] // collections
					};
				ExternalInterface.addCallback( "doCommandFunc", doCommandFromDG ); // make {InferenceGames.swf}.doCommandFromDG() callable from javascript/DG
				this.mDGInterface.initGame( initGameArgs );
			}
			
			// handle a command from DG, such as 'restoreState' or 'saveState'.  
			// 		this.startUp( ) registers this callback function with DG,
			// 		so DG can ask for game state then restore that game state when a DG document is saved then later opened.
			// 		expecting string from DG like: '{ operation: 'restoreState', args: { state: restoredGameState }}'
			public function doCommandFromDG( commandString:String ):String {
				var resultObj:Object = { success: false },
					result:String,
					commandObj:Object = com.adobe.serialization.json.JSON.decode( commandString );
				
				if( commandObj.operation == 'saveState' ) {
					resultObj = this.saveStateforDG( commandObj.args );
				} else if (commandObj.operation == 'restoreState' ) {
					resultObj = this.restoreStateforDG( commandObj.args );
				}		
				if( resultObj )
					result = com.adobe.serialization.json.JSON.encode( resultObj );
				return result;
			}
			
			// handle a 'saveState' command from DG, to stave this game in a document  
			// 		We return information to be passed back to the this game with a restoreStateForDg() call.
			//		Warning: must be compatible with different versions of restoreStateforDG()
			//		Returns { success: true, state: {} }
			private function saveStateforDG( args:Object ):Object {
				var stateObj:Object = 
					{
						success: true,
						state: {
							stateVersion: 1, // increment only if needed by restoreStateForDG()
							gameNumber:	this.mGameSerialNum,
							gameLevel:	this.mGameLevel,
							unlockedTo:	this.unlockedLevels,
							completedTo:this.completedLevels,
							// note: these probably are not needed because we don't restore the current game; but future versions of restore may want them.
							inGame:		this.isInGame,
							roundID:	Round.currentRound.roundID,
							humanScore:	this.sSpaceRace.humanScore,
							expertScore:this.sSpaceRace.expertScore
						}
					};
				return stateObj;
			}
			
			// handle a 'restoreState' command from DG, to restore a game saved in a document.  
			// 		Handles parameters created by a previous save of saveStateForDG()
			//		Warning: must be backward compatible with data created by older versions of saveStateForDG()
			// 		Returns { success: true/false }
			private function restoreStateforDG( args:Object ):Object {
				var iState:Object = args.state,
					result:Object = { success: false };
				if( iState )  // restore our parameters as independently as possible
				{
					if( iState.unlockedTo && iState.unlockedTo >= 1 ) {
						this.unlockedLevels = iState.unlockedTo; // OK if unlockedTo is above Round.kLevelSettings.length
					}
					if( iState.completedTo && iState.completedTo >= 1 ) {
						this.completedLevels = iState.completedTo;
					}
					if( iState.gameNumber ) {
						this.mGameSerialNum = iState.gameNumber;
						// note that we assume a new game will start after restore
					}
					/*
					if( iState.roundID && iState.roundID >= 1 ) {
						//restore roundID
						Round.currentRound = new Round( iState.gameLevel, iState.gameNumber);
						Round.currentRound.roundID = iState.roundID;
					}
					if( iState.humanScore ) {
						sSpaceRace.humanScore = iState.humanScore;
						for(var i:int=0; i<iState.humanScore; i++){
							sSpaceRace.earnPointHuman();
						}
					}
					if( iState.expertScore ) {
						sSpaceRace.expertScore = iState.expertScore;
						for(var j:int=0; j<iState.expertScore; j++){
							sSpaceRace.earnPointExpert();
						}
					}
					*/
					if( iState.gameLevel) {
						if( iState.gameLevel >= 0 && iState.gameLevel < Round.kLevelSettings.length ) { // must be a valid level for this version
							// restore game level and the level label
							mGameLevel = iState.gameLevel;
							sSpaceRace.topBarMVC.setTitleMessage("LEVEL " + mGameLevel);
							sSpaceRace.showMainMenu( unlockedLevels, completedLevels );
						}
						// else ignore invalid levels
					}
					result.success = true;
				}
				return result;
			}			
			
			// get the game case data to send to DG, 
			// returned as an array for use by sendGameDataOpen(),..Update(),...Close().
			public function getGameCaseValues( iIsRoundOver:Boolean = false ):Array {
				var isGameWon:Boolean = Round.currentRound.isWon,
					isGameCanceled:Boolean = iIsRoundOver && (!isGameWon);
				return [
					mGameSerialNum, // Game number starting with 1 since launch of application.
					Round.currentRound.roundID,
					Round.currentRound.level,
					Round.currentRound.StDev,
					Round.currentRound.tolerance,
					(isGameCanceled ? "" : Round.currentRound.numDataSoFar),
					(isGameCanceled ? "game canceled" : Round.currentRound.getResultString()), // Result string, "You Won!/You Lost!", "Expert Won!/Expert Lost!" 
					(isGameWon ? Round.currentRound.accuracy : ""), // if round is over, submit accuracy with current sample size. Otherwise, submit the empty string. 
					(isGameWon ? Round.currentRound.populationMean : ""), // if round is over, show original population mean
					(isGameWon ? Round.currentRound.sampleMean : ""),
					(isGameWon ? sSpaceRace.guess : "")
				];
			}

			// Send event case data to DG, if connected to DG.
			// 		Event data "data point generated" events, with one array value per Event attribute
			// 		caseValueArrays is an array of cases, and each case is a array of attribute values for that case (corresponding to the attributes previously sent)
			//		dataSpeed is the time between single cases in milliseconds; we want sending of data to match data appearance in game
			public function sendEventData( caseValueArrays:Vector.<Number> ):void {
				
				//converts number vector from SpaceRace into array for DG
				var convertArray:Array = new Array();
				for each (var elem:Number in caseValueArrays) {
					convertArray.push([Round.currentRound.roundID, elem]);
				}
				
				// create a timer to send individual data points to DG
				// use the same speed as the points are shown in the Game, so graph/table matches that speed
				var caseCount:int = 0;
				var timer:Timer = new Timer( sSpaceRace.getDataSpeed() );
				timer.addEventListener(TimerEvent.TIMER, sendSingleDataPoint);
				timer.start();
				
				function sendSingleDataPoint(e:Event):void {
					if( timer.currentCount < sSpaceRace.getDataInitialPauseCount() ) {
						trace("data initial pause "+timer.currentCount);
						// wait
					} else if( caseCount < convertArray.length) {
						// send one more point
						mDGInterface.createEventCases( kEventsCollectionName, [convertArray[caseCount]] );
						caseCount++;
					} else {
						mDGInterface.updateOrCloseGameCase( kGamesCollectionName, getGameCaseValues());
						timer.stop();
					}
				}
			}
			
			// ----------------------
			// --- PUBLIC SECTION ---
			// ----------------------
			
			public static function get stage():Stage{
				return staticStage;
			}
			
			// start a new round as requested
			public function newRound():void{
				
				trace("==========newRound()===========");
				
				// make sure that old round is finished
				if(Round.currentRound){
					if(mDGInterface.isGameCaseOpen()) { throw new Error("Old game case not closed before new Round"); }
				}

				// create the new round
				var thisRound:Round = new Round( mGameLevel, mGameSerialNum ); // accessed via Round.currentRound from here forward.
				setGameAndRoundLabel( mGameSerialNum, Round.currentRound.roundID );
				
				// update DG: open and send a new game case for each round
				mDGInterface.sendLog( "newRound:"
					+" round="+Round.currentRound.roundID
					+" population-mean="+Round.currentRound.populationMean
					+" StDev="+Round.currentRound.StDev
					+" tolerance="+Round.currentRound.tolerance
					+" sampleSize="+Round.currentRound.sampleSize
					//+" expert-confidence-percent="+ExpertAI.confidenceIntervalPercent
					//+" expert-guess-N="+ExpertAI.guessNumSamples 
				);
				mDGInterface.openGameCase( kGamesCollectionName, getGameCaseValues());
				mDGInterface.deletePreviousCaseData(); // deletes old graph data to reduce clutter
				
				//send new round info to Space Race
				sSpaceRace.newRound( 
					Round.currentRound.StDev, Round.currentRound.tolerance, Round.currentRound.populationMean, 
					Round.currentRound.sampleSize, Round.currentRound.minOfRange );
			}
			
			// end the round started with newRound()
			public function endRound():void{
				
				// update and close the old round data in DG
				// closing the game case prevents further updating of the Round data.
				if( Round.currentRound && mDGInterface.isGameCaseOpen()) {
					var roundData:Array = getGameCaseValues( DGInterface.kCloseCase );
					mDGInterface.updateOrCloseGameCase( kGamesCollectionName, roundData, DGInterface.kCloseCase );
					trace( "endRound: "+roundData );
				}
			}
			
			// Start a new game.  Each game has multiple rounds
			public function newGame( triggerEvent:Event = null):void {
				sSpaceRace.bodyMVC.controlsMVC.hideMainMenu(); // hide the main menu, if it was showing.
				
				++mGameSerialNum;
				isInGame = true;
								
				mDGInterface.sendLog( "newGame: GameNumber="+mGameSerialNum+" Level="+mGameLevel );
				
				// set up the new game interface which calls this.newRoundHandler() which calls this.NewRound()
				// TO-DO: modify this so newRound() can be called directly to de-tangle the calling logic.
				// and probably the update of possibleIQRs and Intervals should happen every round instead?  (CDM 2012-12-26)	
				sSpaceRace.newGame( Round.kPossibleStDevs, Round.kStartingStDev, Round.kPossibleTolerances, Round.kStartingTolerance, mGameLevel);
				newRoundHandler();
			}
			
			// called when 'end game'/'continue' btn is clicked. Clears the stage, and resets the scores.
			public function endGame( triggerEvent:Event = null):void{      			
				// end the old round if there was one
				if(Round.currentRound){
					endRound();
					Round.currentRound.roundID = 0; // first round of new game is round 1
				}
				setGameAndRoundLabel(); // clear the label
				isInGame = false;
				sSpaceRace.endGame();
				sSpaceRace.showMainMenu( unlockedLevels, completedLevels );
				
				showHumanCursor(); // ensure that cursor is visible at end of game
				
				// check if the expert is up. If he is, hide him.
				if( sSpaceRace.bodyMVC.popupExpertMVC.currentFrame != 1){
					sSpaceRace.bodyMVC.popupExpertMVC.gotoAndPlay("hide");
				}
			}
			
			// display the given Game and Round numbers, or hide the old number display if passed zeros. 
			//		setGameAndRoundLabel(1,3) => "Game 1, Round 3"
			//		setGameAndRoundLabel() => ""
			public function setGameAndRoundLabel( gameNumber:int=0, roundNumber:int=0 ): void {
				if( gameNumber < 1 || roundNumber < 1 ) {
					mGameAndRoundLabel.text = "";
				} else {
					mGameAndRoundLabel.text = "Game "+gameNumber+", Round "+Round.currentRound.roundID;
				}
			}
			
			private function click_lvl1Btn(e:Event):void{
				if( mGameLevel != 1 ) 
					Round.resetNextRoundParams();
				mGameLevel = 1;
				sSpaceRace.topBarMVC.setTitleMessage("LEVEL 1");
				newGame();
			}
			private function click_lvl2Btn(e:Event):void{
				if(unlockedLevels >= 2){	// this is a band-aid. For some reason, the disabled level buttons are still able to be clicked on. So, don't let the method execute unless level 2 is really unlocked.
					if( mGameLevel != 2 ) // if new to this level, restart the sequence of level 2 variable StDev/Tolerances 
						Round.resetNextRoundParams();
					mGameLevel = 2;
					sSpaceRace.topBarMVC.setTitleMessage("LEVEL 2");
					newGame();
				}
			}
			private function click_lvl3Btn(e:Event):void{
				if(unlockedLevels >= 3 ){
					if( mGameLevel != 3 )  // if new to this level, restart the sequence of level 3 variable StDev/Tolerances 
						Round.resetNextRoundParams();
					mGameLevel = 3;
					sSpaceRace.topBarMVC.setTitleMessage("LEVEL 3");
					newGame();
				}
			}
			private function click_lvl4Btn(e:Event):void{
				if(unlockedLevels >= 4){
					if( mGameLevel != 4 ) // if new to this levle, restart the sequence of level 4 variable StDev/Tolerances 
						Round.resetNextRoundParams();
					mGameLevel = 4;
					sSpaceRace.topBarMVC.setTitleMessage("LEVEL 4");
					newGame();
				}
			}
			
			//Video functions
			private function showVideo(e:Event = null):void
			{
				if(IntroVideo.visible == true)
				{
					if(videoPlayer != null)
						videoPlayer.pauseVideo();
					
					IntroVideo.visible = false;
				}
				else
				{
					IntroVideo.visible = true;
					videoPlayer.playVideo();
				}
			}
			private function hideVideo():void
			{
				videoPlayer.pauseVideo();
				IntroVideo.visible = false;
			}
			
			private function toggleAboutScreen(e:Event = null):void{
				aboutScreen.visible = !aboutScreen.visible;
			}
			
			private function playPauseVideo():void
			{
				var PLAYING_VIDEO:Number = 1;
				if(videoPlayer.getPlayerState() == PLAYING_VIDEO) {
					videoPlayer.pauseVideo();
				} else {
					videoPlayer.playVideo();
				}
			}
			private function replayVideo():void
			{
				videoPlayer.seekTo(0, true);
				videoPlayer.playVideo();
			}
			private function onVideoStateChange(e:Event):void
			{
				var PLAY:int=1, PAUSE:int=2;
				var state:int = Object(e).data;
				switch(state)
				{
					case PLAY:
						playPauseBtn.label = "Pause"; break;
					case PAUSE:
						playPauseBtn.label = "Play"; break;
				}
			}			
			
			// toggles the visibilty of the about screen
			private function showHideAboutScreen( triggerEvent:MouseEvent):void{
				aboutScreen.visible = !aboutScreen.visible;
			}
		]]>
	</fx:Script>
	
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		.sansFontClass {
			fontFamily: Arial, Helvetica, "_sans";
			fontSize: 12;
		}
	</fx:Style>
	
	<!-- the game's background -->
	<s:BorderContainer id="background" y="36" width="575" height="255">
		<s:backgroundFill>
			 <!-- <s:RadialGradient scaleX="1000" scaleY="1000">
				<s:GradientEntry color="0xcad7eb"/>
				<s:GradientEntry color="0x8C7A8B"/>
			</s:RadialGradient> -->
			<s:SolidColor color="0xbfcfe6"/>
		</s:backgroundFill>
	</s:BorderContainer>
	
	<!-- the UI component that holds all the game's SWC's -->
	<mx:UIComponent id="gameContainer" x="0" y="0"/>
	
	<s:Label id="mVersionLabel" x="{(this.width+mVersionLabel.width)/2}" y="5" width="100" height="50"
			 color="0x777777" initialize="mVersionLabel.text=kVersion" 
			 fontFamily="_sans" fontSize="9" textAlign="center" verticalAlign="top"/>
	<s:Label id="mGameAndRoundLabel" x="14" y="22" width="100" height="50"
			 color="0x8fa0c1"
			 fontFamily="_sans" fontSize="9" textAlign="left" verticalAlign="top"/>
	
	<!-- Contains Intro video -->
	<s:BorderContainer id="IntroVideo" x="0" y="25" width="{this.width}" height="{this.height-25}" visible="false" backgroundColor="#dcd0c0" styleName="sansFontClass" borderVisible="false" depth="700">
		<mx:UIComponent	id="videoHolder" x="{(this.width-570)/2}" y="0"/>
		<s:Group id="videoControlsContainer" x="{(this.width-this.videoControlsContainer.width)/2}" y="{this.height-this.videoControlsContainer.height-25}">
			<s:Button id="playPauseBtn" label="Pause" click="playPauseVideo()" x="0" />
			<s:Button id="replayBtn" label="Replay" click="replayVideo()" x="{playPauseBtn.width + 5}"/>
			<s:Button id="closeVideoBtn" label="Close" click="hideVideo()" x="{playPauseBtn.width+replayBtn.width+20}" />
		</s:Group>
	</s:BorderContainer>
	
	<mx:UIComponent	id="videoBtnContainer" x="0" y="0"/>

	<!-- About Screen -->
	<s:BorderContainer id="aboutScreen" x="{30}" y="{30}" width="{this.width-(2*aboutScreen.x)}" height="{this.height-(2*aboutScreen.y)}" cornerRadius="5" styleName="sansFontClass" visible="false" depth="800">
		<s:filters>
			<s:DropShadowFilter 
				blurX="25" blurY="25" 
				alpha="0.8" />
		</s:filters>
		<s:backgroundFill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="0xf0f0f0"/>
				<s:GradientEntry color="0xd8d8d8"/>
			</s:LinearGradient>
		</s:backgroundFill>
		<s:Label text="About Inference" width="{aboutScreen.width}" y="20" fontSize="22" textAlign="center"/>
		<mx:Text selectable="true" x="50" y="50" width="{aboutScreen.width-100}" htmlText="{kCreditsTextHTML}" fontSize="13" />
		<s:Button x="{(aboutScreen.width-90)/2}" y="215" width="90" height="30" label="OK" click="aboutScreen.visible=false;" fontSize="12" fontWeight="bold"/>
	</s:BorderContainer>

	<!-- Error message for bad version of flash player -->	
	<s:BorderContainer id="BrickWall" visible="false" x="0" y="0"
					   width="575"
					   height="327" depth="999">
		<s:backgroundFill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="0x13B3C2"/>
				<s:GradientEntry color="0x1E5B77"/>
			</s:LinearGradient>
		</s:backgroundFill>
		
		<s:Label id="topLabelError" x="0" y="30" width="575"
				 color="0xffffff" fontFamily="Georgia" fontSize="20" fontWeight="bold"
				 text="You are using an outdated version of Adobe Flash Player." textAlign="center"/>
		<s:Label id="midLabelError" x="0" y="100" width="575"
				 color="0xffffff" fontFamily="Georgia" fontSize="15"
				 text="{'Inference Games requires Flash Player '+ kMinimumRequiredVersion +' or later.'}"
				 textAlign="center"/>
		<s:Label id="midLabel2Error" x="0" y="120" width="575"
				 color="0xffffff" fontFamily="Georgia" fontSize="15"
				 text="{'You are currently using Flash Player ' + VersionChecker.getVersion()}"
				 textAlign="center"/>
		<s:Button id="upgradeButton" x="{(575-350)/2}" y="190" width="350"
				  height="50" fontFamily="Georgia" label="Upgrade Flash Player" click="VersionChecker.openAdobeWebsite()"
				  enabled="true" fontSize="25"/>
		<s:Button id="ignoreButton" x="{(575-350)/2}" y="250" width="350"
				  height="30" fontFamily="Georgia" label="Ignore Warning" click="startUp()" enabled="true" fontSize="12"/>
	</s:BorderContainer>
	
</s:Application>
